0  BEGIN PGM NIERE_30072015 MM 
1  BLK FORM 0.1 Z X+0 Y+0 Z-20
2  BLK FORM 0.2  X+100  Y+100  Z+0
3  CYCL DEF 247 BEZUGSPUNKT SETZEN ~
    Q339=+1    ;BEZUGSPUNKT-NUMMER
4  TOOL CALL "SCHFR_DM10_SN3" Z S8912.677 F1069.521
5  ;
6  L  Z+100 FMAX M13
7  ;
8  CYCL DEF 14.0 KONTUR
9  CYCL DEF 14.1 KONTURLABEL1 /2
10 ;
11 CYCL DEF 20 KONTUR-DATEN ~
    Q1=-18   ;FRAESTIEFE ~
    Q2=+1    ;BAHN-UEBERLAPPUNG ~
    Q3=+0    ;AUFMASS SEITE ~
    Q4=+0    ;AUFMASS TIEFE ~
    Q5=+0    ;KOOR. OBERFLAECHE ~
    Q6=+2    ;SICHERHEITS-ABST. ~
    Q7=+10   ;SICHERE HOEHE ~
    Q8=+0.2  ;RUNDUNGSRADIUS ~
    Q9=-1    ;DREHSINN
12 ;
13 CYCL DEF 22 AUSRAEUMEN ~
    Q10=-9    ;ZUSTELL-TIEFE ~
    Q11= AUTO ;VORSCHUB TIEFENZ. ~
    Q12= AUTO ;VORSCHUB RAEUMEN ~
    Q18=+0    ;VORRAEUM-WERKZEUG ~
    Q19=+0    ;VORSCHUB PENDELN ~
    Q208= MAX ;VORSCHUB RUECKZUG
14 ;
15 CYCL CALL
16 ;
17 L  Z+100 FMAX
18 M30
19 ;
20 ;
21 LBL 1
22 L  X+15  Y+20 RL
23 FC R10 DR-  CCX+15  CCY+30
24 FCT DR- R80
25 FCT DR- R20  CCX+60  CCY+50
26 FSELECT2
27 FCT DR+ R75
28 FCT  X+15  Y+20 DR- R10  CCX+15  CCY+30
29 FSELECT1
30 LBL 0
31 ;
32 LBL 2
33 L  X-10  Y-10 RR
34 L IY+120
35 L IX+120
36 L IY-120
37 L IX-120
38 LBL 0
39 END PGM NIERE_30072015 MM 
