0  BEGIN PGM MATRIZE2_23072015 MM 
1  BLK FORM 0.1 Z X+0 Y+0 Z-30
2  BLK FORM 0.2  X+120  Y+100  Z+0
3  CYCL DEF 247 BEZUGSPUNKT SETZEN ~
    Q339=+1    ;BEZUGSPUNKT-NUMMER
4  TOOL CALL "SCHFR_DM10_SN3" Z S6366.198 F1527.888
5  L  Z+100 FMAX M13
6  CYCL DEF 14.0 KONTUR
7  CYCL DEF 14.1 KONTURLABEL1 /2 /3 /4
8  CYCL DEF 20 KONTUR-DATEN ~
    Q1=-20   ;FRAESTIEFE ~
    Q2=+1    ;BAHN-UEBERLAPPUNG ~
    Q3=+0    ;AUFMASS SEITE ~
    Q4=+0    ;AUFMASS TIEFE ~
    Q5=+0    ;KOOR. OBERFLAECHE ~
    Q6=+2    ;SICHERHEITS-ABST. ~
    Q7=+10   ;SICHERE HOEHE ~
    Q8=+0.5  ;RUNDUNGSRADIUS ~
    Q9=-1    ;DREHSINN
9  CYCL DEF 22 AUSRAEUMEN ~
    Q10=-5    ;ZUSTELL-TIEFE ~
    Q11= AUTO ;VORSCHUB TIEFENZ. ~
    Q12= AUTO ;VORSCHUB RAEUMEN ~
    Q18=+0    ;VORRAEUM-WERKZEUG ~
    Q19= AUTO ;VORSCHUB PENDELN ~
    Q208=+99999 ;VORSCHUB RUECKZUG
10 M99
11 M30
12 LBL 1
13 L  X+50  Y+70 RR
14 L  Y+90
15 RND R10
16 L IX+60
17 RND R10
18 L  Y+10
19 RND R10
20 L IX-60
21 RND R10
22 L  Y+70
23 LBL 0
24 LBL 3
25 L  X+80  Y+50 RL
26 L  Y+70
27 CR IX+15 IY+0 R+7.5 DR-
28 L IY-40
29 CR IX-15 IY+0 R+7.5 DR-
30 L  Y+50
31 LBL 0
32 LBL 2
33 L  X+10  Y+35 RR
34 CC  X+35  Y+35
35 CR IX+50 IY+0 R+25 DR-
36 CR IX-50 IY+0 R+25 DR-
37 LBL 0
38 LBL 4
39 L  X+27.5  Y+35 RL
40 CC  X+35  Y+35
41 CR IX+15 IY+0 R+7.5 DR-
42 CR IX-15 IY+0 R+7.5 DR-
43 LBL 0
44 END PGM MATRIZE2_23072015 MM 
