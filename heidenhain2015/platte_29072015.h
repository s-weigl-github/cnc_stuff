0  BEGIN PGM PLATTE_29072015 MM 
1  BLK FORM 0.1 Z X+0 Y+0 Z-20
2  BLK FORM 0.2  X+100  Y+100  Z+0
3  CYCL DEF 247 BEZUGSPUNKT SETZEN ~
    Q339=+1    ;BEZUGSPUNKT-NUMMER
4  TOOL CALL "SCHFR_DM8_SN2" Z S11140.846 F891.268
   ;
5  L  Z+100 FMAX M13
6  L  Z+10
7  ;
8  ;
9  CYCL DEF 14.0 KONTUR
10 CYCL DEF 14.1 KONTURLABEL1
11 ;
12 CYCL DEF 20 KONTUR-DATEN ~
    Q1=-5    ;FRAESTIEFE ~
    Q2=+1    ;BAHN-UEBERLAPPUNG ~
    Q3=+0    ;AUFMASS SEITE ~
    Q4=+0    ;AUFMASS TIEFE ~
    Q5=+0    ;KOOR. OBERFLAECHE ~
    Q6=+2    ;SICHERHEITS-ABST. ~
    Q7=+10   ;SICHERE HOEHE ~
    Q8=+0.2  ;RUNDUNGSRADIUS ~
    Q9=-1    ;DREHSINN
13 ;
14 CYCL DEF 22 AUSRAEUMEN ~
    Q10=-5    ;ZUSTELL-TIEFE ~
    Q11= AUTO ;VORSCHUB TIEFENZ. ~
    Q12= AUTO ;VORSCHUB RAEUMEN ~
    Q18=+0    ;VORRAEUM-WERKZEUG ~
    Q19=+0    ;VORSCHUB PENDELN ~
    Q208= MAX ;VORSCHUB RUECKZUG
15 ;
16 ;
17 CYCL DEF 7.0 NULLPUNKT
18 CYCL DEF 7.1  X+10
19 CYCL DEF 7.2  Y+10
20 ;
21 CYCL CALL
22 ;
23 ;
24 CYCL DEF 7.0 NULLPUNKT
25 CYCL DEF 7.1  X+60
26 CYCL DEF 7.2  Y+60
27 ;
28 CYCL DEF 10.0 DREHUNG
29 CYCL DEF 10.1  ROT+20
30 ;
31 CYCL CALL
32 ;
33 CYCL DEF 10.0 DREHUNG
34 CYCL DEF 10.1  ROT+0
35 ;
36 ;
37 CYCL DEF 7.0 NULLPUNKT
38 CYCL DEF 7.1  X+80
39 CYCL DEF 7.2  Y+10
40 ;
41 CYCL DEF 8.0 SPIEGELUNG
42 CYCL DEF 8.1 X
43 ;
44 CYCL CALL
45 ;
46 CYCL DEF 8.0 SPIEGELUNG
47 CYCL DEF 8.1
48 ;
49 ;
50 CYCL DEF 7.0 NULLPUNKT
51 CYCL DEF 7.1  X+10
52 CYCL DEF 7.2  Y+60
53 ;
54 CYCL DEF 11.0 MASSFAKTOR
55 CYCL DEF 11.1 SCL 1.2
56 ;
57 CYCL CALL
58 ;
59 ;
60 L  Z+100 FMAX
61 M30
62 ;
63 ;
64 LBL 1
65 L  X+15  Y+20 RR
66 L IX+15
67 RND R5
68 L IX-10 IY-20
69 RND R5
70 L IX-10
71 CR IX+0 IY+20 R+10 DR-
72 L IX+5
73 LBL 0
74 END PGM PLATTE_29072015 MM 
